class_name StateMachine
extends Node

@export var initial_state: State

var current_state: State
var states: Dictionary[String, State]


func _ready() -> void:
	for child in get_children():
		if child is State:
			states[child.name.to_lower()] = child
			child.transition_requested.connect(on_child_transition.bind(child))

	if initial_state:
		current_state = initial_state
		current_state.enter()
	else:
		push_warning("No initial state on state machine attached to " + get_parent().name)


func _process(delta: float) -> void:
	if current_state:
		current_state.update(delta)


func _physics_process(delta: float) -> void:
	if current_state:
		current_state.physics_update(delta)


func on_child_transition(new_state_name: String, state: State)  -> void:
	if state != current_state:
		return

	var new_state: State = states.get(new_state_name.to_lower())
	if not new_state:
		push_warning("couldn't find state named " + new_state_name)
		return

	current_state.exit()
	current_state = new_state
	current_state.enter()
